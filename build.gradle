buildscript {
    ext {
        springBootVersion = '2.5.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "org.sonarqube"

def getVersionName = { ->
    if (System.getenv('GIT_COMMIT') != null) {
        return System.getenv('GIT_COMMIT')
    } else if (System.getenv('BUILD_NUMBER') != null) {
        return System.getenv('BUILD_NUMBER')
    }
    return "SNAPSHOT"
}

group = 'ch.puzzle'
version = '0.0.1-' + getVersionName()
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

jar {
    bootJar.enabled = true
    jar.enabled = false
}

bootJar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': archiveVersion,
                'Implementation-Timestamp': new Date().format("dd.MM.yyy HH:mm:ss"),
                'Jenkins-buildnumber': (System.getenv('BUILD_NUMBER') == null) ? "none" : System.getenv('BUILD_NUMBER'),
                'Buildjob-name': (System.getenv('JOB_NAME') == null) ? "none" : System.getenv('JOB_NAME'),
                'Buildjob-url': (System.getenv('BUILD_URL') == null) ? "none" : System.getenv('BUILD_URL'),
                'Git-repourl': (System.getenv('GIT_REPO_URL') == null) ? "none" : System.getenv('GIT_REPO_URL'),
                'Git-commit': (System.getenv('GIT_COMMIT') == null) ? "none" : System.getenv('GIT_COMMIT'),
                'Git-branch': (System.getenv('BRANCH_NAME') == null) ? "none" : System.getenv('BRANCH_NAME')

    }
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-oauth2-client')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf:')
    implementation('io.springfox:springfox-boot-starter:3.0.0')
    implementation('org.springframework.hateoas:spring-hateoas')
    implementation('org.liquibase:liquibase-core')
    implementation('com.fasterxml.jackson.datatype:jackson-datatype-hibernate5')
    implementation('org.xhtmlrenderer:flying-saucer-pdf:9.1.16')
    implementation('io.sentry:sentry-spring:1.7.20')
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')
    runtimeOnly('org.postgresql:postgresql')
    runtimeOnly('io.micrometer:micrometer-registry-prometheus')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation('com.h2database:h2')
}

test {
    useJUnitPlatform()
    maxHeapSize = '1G'
}